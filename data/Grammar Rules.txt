// 此处用的是BNF范式，其能力等价于上下文无关文法

// { ... } 至少重复0次
// [ ... ] 重复出现0次或者1次
// ... | ... 与其中之一匹配
// () 括号内视为一个完整的模式

// 基本构成元素, 这里添加的postfix是对函数调用形式的支持
primary   :  ( "(" expr ")" { postfix } ) | NUMBER | ( IDENTIFIER { postfix } ) | STRING )
// 因子
factor    :   "-" primary | primary
// 表达式
expr      :   factor { OP factor }
// 代码块
block     :   "{" [ statement ] { (";" | EOL) [ statement ] } "}"
// 简单表达式语句
simple    :   expr
// 语句
statement :   "if" expr block [ "else" block ] | "while" expr block | simple
// 程序
program   :   [ func | statement ] (";" | EOL)
// 函数参数
param : IDENTIFIER
// 参数列表
params : param { "," param }
// 参数部分
param_list: "(" [ params ] ")"
// 函数定义
func : "function" IDENTIFIER param_list block
// 函数调用参数列表
args : expr { "," expr }
// 函数调用参数部分
postfix : "(" [ args ] ")"
